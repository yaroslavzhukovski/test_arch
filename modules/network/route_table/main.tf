resource "azurerm_route_table" "this" {
  name                = var.name
  location            = var.location
  resource_group_name = var.resource_group_name
  tags                = var.tags
}

resource "azurerm_route" "this" {
  for_each            = var.routes
  name                = each.key
  resource_group_name = var.resource_group_name
  route_table_name    = azurerm_route_table.this.name

  address_prefix = each.value.address_prefix
  next_hop_type  = each.value.next_hop_type

  # only required for VirtualAppliance
  next_hop_in_ip_address = (
    each.value.next_hop_type == "VirtualAppliance"
    ? each.value.next_hop_ip
    : null
  )
}
resource "azurerm_subnet_route_table_association" "this" {
  for_each       = var.subnet_ids
  subnet_id      = each.value
  route_table_id = azurerm_route_table.this.id
}
